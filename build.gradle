/*
=========================================================
Buildscript
=========================================================
*/
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath('org.springframework.boot:spring-boot-gradle-plugin:1.4.3.RELEASE')
    }
}

/*
=========================================================
Apply plugins
=========================================================
*/
apply plugin: 'java'
apply plugin: 'maven'
//apply plugin: 'maven-publish'
apply plugin: 'org.springframework.boot'

/*
=========================================================
Repo Info
=========================================================
*/
group = 'com.vite'
description = "Vite backend server"


/*
=========================================================
  Create self contained jar
=========================================================
*/

def artifactName = 'vite_server'

springBoot {
    mainClass = "com.vite.Application"
}

bootRun {
/*
 * Support testing directly via gradle.
 *
 * Example:
 *  $ gradle bootRun -Pargs="-d mem -t gui"
 */
    if (project.hasProperty('args')) {
        args project.args.split('\\s+')
    }
}

task generatePom << {
    pom {
        project {
            artifactId = artifactName
            name = artifactName
        }
    }.writeTo("$buildDir/tmp/maven/pom.xml")
}

jar {
    baseName = artifactName
    manifest {
        attributes(
                "Implementation-Title": baseName,
                "Implementation-Version": version,
        )
    }
    into("META-INF/maven/$project.group/$baseName") {
        from "$buildDir/tmp/maven/"
    }
}

jar.dependsOn generatePom

/*
=========================================================
    Javadocs Generation
=========================================================
*/
task docs(type: Javadoc) {
    source = sourceSets.main.allJava
    destinationDir = file("$project.buildDir/javadocs")
    failOnError = false
}

task docBundle(type: Tar) {
    baseName = artifactName
    version = project.version
    extension = "tar.gz"
    classifier = "javadoc"
    destinationDir = file("$buildDir/")
    from(project.files("$buildDir/javadocs"))
}
docBundle.dependsOn docs

/*
=========================================================
   Load Dependencies
=========================================================
*/

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile(group: 'org.springframework.boot', name: 'spring-boot-starter-web')
    compile(group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.7')
    compile(group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.7')
    compile(group: 'com.vite', name: 'vite-server-api', version: '0.0.0-SNAPSHOT')

    testCompile(group: 'org.hamcrest', name: 'hamcrest-core', version: '1.3')
    testCompile(group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3')
    testCompile(group: 'junit', name: 'junit', version: '4.12')
    testCompile(group: 'org.mockito', name: 'mockito-core', version: '1.8.5')
}
